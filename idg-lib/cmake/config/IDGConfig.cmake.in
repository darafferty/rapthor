#  Config file for the IDG package
#  https://cmake.org/Wiki/CMake/Tutorials/How_to_create_a_ProjectConfig.cmake_file
#  It defines the following variables
#  IDG_INCLUDE_DIR         - include directories
#  IDG_CPU_LIB             - libraries to link against
#  IDG_CUDA_LIB            - libraries to link against
#  IDG_HYBRID_CUDA_LIB     - libraries to link against
#  IDG_OPENCL_LIB          - libraries to link against
#  IDG_KNC_LIB             - libraries to link against
#  IDG_PYTHON_MODULE_PATH  - path to 'idg' python module
#  IDG_UTILITY_INCLUDE_DIR - include directories
#  IDG_UTILITY_LIB         - libraries to link against
#  It works for the following scenario:
#  (1) install IDG using CMAKE_INSTALL_PREFIX, say $HOME/opt/idg
#  (2) use the installed library in the cmake project as
#      find_package(IDG NO_MODULE)
#  (3) run such as $ cmake -DIDG_DIR=$HOME/opt/idg ..
#  (4) use for instance
#      include_directories(${IDG_INCLUDE_DIR} ${IDG_UTILITY_INCLUDE_DIR})
#      target_link_libraries(a.out idg-cpu idg-utility)

# Compute paths
get_filename_component(IDG_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)
get_filename_component(IDG_PREFIX_DIR "${IDG_CMAKE_DIR}" ABSOLUTE)

set(IDG_INCLUDE_DIR "${IDG_PREFIX_DIR}/include"
    CACHE PATH "IDG include directory")

set(IDG_LIB_PATH "${IDG_PREFIX_DIR}/lib"
    CACHE PATH "IDG library directory")


# Our library dependencies (contains definitions for IMPORTED targets)
include("${IDG_PREFIX_DIR}/lib/cmake/IDGTargets.cmake")

if (@BUILD_LIB_CPU@)
set(IDG_CPU_LIB idg-cpu CACHE LOCATION "IDG CPU library")
endif()

if (@BUILD_LIB_CUDA@)
set(IDG_CUDA_LIB idg-cuda CACHE LOCATION "IDG CUDA library")
endif()

if (@BUILD_LIB_OPENCL@)
set(IDG_OPENCL_LIB idg-opencl CACHE LOCATION "IDG OpenCL library")
endif()

if (@BUILD_LIB_CPU@ AND @BUILD_LIB_CUDA@)
set(IDG_HYBRID_CUDA_LIB idg-hybrid-cuda CACHE LOCATION "IDG hybrid CUDA library")
endif()

if (@BUILD_LIB_KNC@)
set(IDG_KNC_LIB idg-knc CACHE LOCATION "IDG KNC library")
endif()

if (@BUILD_WITH_PYTHON@)
set(IDG_PYTHON_MODULE_PATH "${IDG_PREFIX_DIR}/lib/python2.7/dist-packages"
    CACHE PATH "IDG python module path")
endif()

if (@BUILD_WITH_EXAMPLES@ OR @BUILD_WITH_TESTS@)
set(IDG_UTILITY_INCLUDE_DIR "${IDG_PREFIX_DIR}/include/utility"
    CACHE PATH "IDG utility include directory")
set(IDG_UTILITY_LIB idg-utility CACHE LOCATION "IDG utility library")
endif()
